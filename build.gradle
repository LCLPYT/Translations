plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'gradle-build-utils' version '1.7.0'
}

Properties props = buildUtils.loadProperties('publish.properties')

group = project.maven_group
version = buildUtils.gitVersion()
archivesBaseName = project.archives_base_name

repositories {
    mavenCentral()

    maven {
        url = 'https://repo.lclpnet.work/repository/internal'
    }
}

sourceSets {
    network {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }

    test {
        compileClasspath += network.output + network.compileClasspath
        runtimeClasspath += network.output + network.runtimeClasspath
    }
}

java {
    withSourcesJar()

    registerFeature('network') {
        usingSourceSet(sourceSets.network)
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    networkCompileOnly(compileOnly('org.jetbrains:annotations:24.1.0'))
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.slf4j:slf4j-api:2.0.6'
    networkApi 'work.lclpnet:lclpnetwork-api:1.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.mockito:mockito-core:3.+'
}

configurations {
    networkImplementation {
        extendsFrom implementation
    }
}

test {
    useJUnitPlatform()
}

/*
 * Copyright (c) 2024 LCLP.
 *
 * Licensed under the MIT License. For more information, consider the LICENSE file in the project's root directory.
 */

// tweak jar tasks
[jar, sourcesJar, networkJar].each {
    // include license file
    it.from('LICENSE') {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// support utf-8 java source
tasks.withType(JavaCompile).each {
    it.options.encoding = "UTF-8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archives_base_name

            from components.java

            pom {
                name = 'translations4j'
                description = 'Library to manage I18n with Java applications.'
            }
        }
    }

    buildUtils.setupPublishRepository(repositories, props)
}